# Cormack-Jolly-Seber model with survival with time-since-marking 
# on survival and capture probabilities - 
# converted from trap response model on p.212 of Kery and 
# Schaub
# There are two survival rates, which correspond to the year after the first marking and the rest. 
# There also are two capture probabilities, which correspond to the first marking and the rest. 
#
# The time since marking (TSM) models are described well in Chapter 7
# of the MARK book. Starting around p. 7-40, they describe how the survival
# rates of the first and the rest provide evidence for transients. In the
# sidebar on p. 7-44 explains how the ratio between the two estimated
# survival rates from a TSM model can be used to estimate the proportion of 
# transients (or residents).  

model {

  for (i in 1:nind){
     mu[i] ~ dbeta(5, 2) #unif(0,1)
	 
	 # make capture probability of an individual prior to its first
	 # capture (f[i]) to be zero, rather than NA so that we can sum
	 # later for Nhat.
	 for (t in 1:(f[i]-1)){
		p[i,t] <- 0
	}
	 
     for (t in f[i] : (n.occasions-1)){
        phi[i, t] <- gamma[m[i,t]]
        logit(p[i, t]) <- logit(mu[i]) + beta[m[i,t]] * x[t] + epsilon[i,t]
		epsilon[i,t] ~ dnorm(0, 1/var_logitP)
     }
  }

  for (t in 1:(n.occasions-1)){
    N[t] <- sum(1/(inprod(y[,t], p[,t])))
  }
  
  for (u in 1:2){
     gamma[u] ~ dbeta(5, 2)
     beta[u] ~ dbeta(5, 2)
  }

  prop.trans <- 1 - (gamma[1]/gamma[2])
  
  #mean.p ~ dbeta(5, 2) #unif(0,1)
  #mu <- log(mean.p / (1 - mean.p))
	
  #beta ~ dnorm(0, 0.01)
  var_logitP ~ dgamma(5, 0.1)
  sigma_logitP <- sqrt(var_logitP)

  # Likelihood:
  for (i in 1:nind){
    # define latent state at first capture
    z[i, f[i]] <- 1
    for (t in (f[i] + 1):n.occasions){
      # state process
      z[i,t] ~ dbern(mu1[i,t])
      mu1[i,t] <- (phi[i,t-1] ^ dt[t]) * z[i, t-1]

      # observation process
      y[i,t] ~ dbern(mu2[i,t])
      mu2[i,t] <- p[i,t-1] * z[i,t]
      loglik[i,t] <- logdensity.bern(y[i,t], mu2[i,t])
    }
  }

}


